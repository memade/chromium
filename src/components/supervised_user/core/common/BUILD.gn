# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//components/supervised_user/buildflags.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_SUPERVISED_USERS=$enable_supervised_users" ]
}

static_library("common") {
  sources = [
    "features.cc",
    "features.h",
    "pref_names.cc",
    "pref_names.h",
    "supervised_user_constants.cc",
    "supervised_user_constants.h",
    "supervised_user_denylist.cc",
    "supervised_user_denylist.h",
    "supervised_users.h",
  ]

  deps = [
    ":buildflags",
    "//base",
    "//url",
  ]

  if (enable_supervised_users) {
    sources += [
      "supervised_user_settings_service.cc",
      "supervised_user_settings_service.h",
    ]

    deps += [
      "//components/prefs:prefs",
      "//components/sync",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "features_unittest.cc" ]
  deps = [
    ":common",
    "//base",
    "//base/test:test_support",
    "//testing/gtest",
  ]

  if (enable_supervised_users) {
    sources += [ "supervised_user_settings_service_unittest.cc" ]
    deps += [
      "//components/prefs:test_support",
      "//components/sync:test_support",
      "//content/test:test_support",
    ]
  }
}
